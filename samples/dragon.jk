vars Dragon, pet

Dragon = {name|
  vars asleep, stuff-in-belly, stuff-in-intestine,
       feed, walk, put-to-bed, toss, rock,
       hungry?, poopy?, passage-of-time

  asleep = false
  stuff-in-belly     = 10 -- He's full.
  stuff-in-intestine =  0 -- He doesn't need to go.

  print(name + ' is born.')

  feed = {|
    print('You feed ' + name + '.')
    stuff-in-belly = 10
    passage-of-time()
  }

  walk = {|
    print('You walk ' + name + ".")
    stuff-in-intestine = 0
    passage-of-time
  }

  put-to-bed = {|
    print('You put ' + name + ' to bed.')
    asleep = true
    for i in range(3) {
      if asleep {
        passage-of-time()
      }
      if asleep {
        print(name + ' snores, filling the room with smoke.')
      }
    }
    if asleep {
      asleep = false
      print(name + ' wakes up slowly.')
    }
  }

  toss = {|
    print('You toss ' + name + ' up into the air.')
    print('He giggles, which singes your eyebrows.')
    passage-of-time()
  }

  rock = {|
    print('You rock ' + name + ' gently.')
    asleep = true
    print('He briefly dozes off...')
    passage-of-time()
    if asleep {
      asleep = false
      print('...but wakes when you stop.')
    }
  }

  hungry? = {|
    stuff-in-belly <= 2
  }

  poopy? = {|
    stuff-in-intestine >= 8
  }

  passage-of-time = {|
    if stuff-in-belly > 0 {
      -- Move food from belly to intestine
      stuff-in-belly     = stuff-in-belly     - 1
      stuff-in-intestine = stuff-in-intestine + 1
    } else { -- Our dragon is starving!
      if asleep {
        asleep = false
        print('He wakes up suddenly!')
      }
      print(name + ' is starving! In desperation, he ate YOU!')
      abort "died"
    }

    if stuff-in-intestine >= 10 {
      stuff-in-intestine = 0
      print('Whoops! ' + name + ' had an accident...')
    }

    if hungry?() {
      if asleep {
        asleep = false
        print('He wakes up suddenly!')
      }
      print(name + "'s stomach grumbles...")
    }

    if poopy?() {
      if asleep {
        asleep = false
        print('He wakes up suddenly!')
      }
      print(name + ' does the potty dance...')
    }
  }

  -- Export the public interface to this closure object.
  {
   feed: feed
   walk: walk
   put-to-bed: put-to-bed
   toss: toss
   rock: rock
  }

}

pet = Dragon('Norbert')
pet.feed()
pet.toss()
pet.walk()
pet.put-to-bed()
pet.rock()
pet.put-to-bed()
pet.put-to-bed()
pet.put-to-bed()
pet.put-to-bed()
